// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Getting the Load bit
    Or(a=reset, b=load, out=resetORload);
    Or(a=resetORload, b=inc, out=loadBit);

    // Incrementing the previous value
    Inc16(in=previous, out=incremented);

    // Selecting the result
    Mux16(a=in, b=incremented, sel=inc, out=incrementedORin);
    Mux16(a=incrementedORin, b=in, sel=load, out=loadORincrement);
    Mux16(a=loadORincrement, b=false, sel=reset, out=registerValue);

    Register(in=registerValue, load=loadBit, out=out, out=previous);
}
